dnl Process this file with autoconf to produce a configure script.
AC_INIT(app/diagram.c)

AM_INIT_AUTOMAKE(dia, 0.41)

dnl Specify a header configuration file
AM_CONFIG_HEADER(config.h)

dnl Initialize maintainer mode
AM_MAINTAINER_MODE

dnl Initialize libtool
AM_DISABLE_STATIC
AM_PROG_LIBTOOL

dnl Build time sanity check
AM_SANITY_CHECK

dnl Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_ISC_POSIX

dnl Checks for libraries.
AM_PATH_GTK(1.2.0)

dnl Checks for dynamic linking

AC_CHECK_LIB(dl,dlopen)
if test "$ac_cv_lib_dl_dlopen" = "yes"; then
  AC_CHECK_FUNCS(dlopen)
else
AC_CHECK_FUNCS(dlopen)
if test "$ac_cv_func_dlopen" = "no"; then
  AC_MSG_ERROR(Can't find dlopen.)
fi
fi

DIA_DLSYM_USCORE

dnl
dnl GNOME
dnl

AC_MSG_CHECKING([whether GNOME specific code should be used])
AC_ARG_ENABLE(gnome,
[  --enable-gnome          enable gnome code],
    GNOME=$enableval,
    GNOME=no)
AC_MSG_RESULT($GNOME)
GNOME_CFLAGS=""
GNOME_LIBS=""
if test "$GNOME" = "yes"; then
  AC_DEFINE(GNOME)
  GNOME_CFLAGS=`gnome-config gnomeui --cflags`
  GNOME_LIBS=`gnome-config gnomeui --libs`
fi
AC_SUBST(GNOME_CFLAGS)
AC_SUBST(GNOME_LIBS)

dnl
dnl Popt
dnl

AC_CHECK_LIB(popt, poptPeekArg)
AC_CHECK_HEADERS(popt.h)

dnl
dnl Debugging
dnl

AC_ARG_ENABLE(debug, 
[  --enable-debug=[no/minimum/yes] turn on debugging [default=minimum]],
  ,
  enable_debug=minimum)

if test "x$enable_debug" = "xyes"; then
  DEBUG_FLAGS="-g -DENABLE_DEBUG"
else
  if test "x$enable_debug" = "xno"; then
    DEBUG_FLAGS=""
  else
    DEBUG_FLAGS=""
  fi
fi
AC_SUBST(DEBUG_FLAGS)

dnl
dnl i18n
dnl

ALL_LINGUAS="da de en_GB hu no pl sv fr"
AM_GNU_GETTEXT
localedir='${prefix}/${DATADIRNAME}/locale'
AC_SUBST(localedir)

dnl	
dnl Locate the gnome-xml library
dnl
AC_CHECK_PROG(XML_CONFIG, xml-config, xml-config)
if test "$XML_CONFIG" != "" 
then
  XML_LIBS="`$XML_CONFIG --libs`"
  XML_CFLAGS="`$XML_CONFIG --cflags`"
else
  AC_MSG_ERROR(Can't find gnome-xml library.)
fi
AC_SUBST(XML_LIBS)
AC_SUBST(XML_CFLAGS)

dnl
dnl Locate the imlib library
dnl
AM_PATH_GDK_IMLIB()
AC_SUBST(GDK_IMLIB_LIBS)
AC_SUBST(GDK_IMLIB_CFLAGS)

dnl Checks for header files.
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h unistd.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST

dnl Checks for library functions.
AC_CHECK_FUNCS(select strcspn strdup strtol snprintf)

AC_OUTPUT(
Makefile
lib/Makefile
lib/pixmaps/Makefile
intl/Makefile
po/Makefile.in
objects/Makefile
objects/standard/Makefile
objects/standard/pixmaps/Makefile
objects/network/Makefile
objects/network/pixmaps/Makefile
objects/UML/Makefile
objects/UML/pixmaps/Makefile
objects/ER/Makefile
objects/ER/pixmaps/Makefile
objects/FS/Makefile
objects/FS/pixmaps/Makefile
objects/sybase/Makefile
objects/sybase/pixmaps/Makefile
objects/flowchart/Makefile
objects/flowchart/pixmaps/Makefile
objects/custom/Makefile
objects/custom/pixmaps/Makefile
objects/custom/Circuit/Makefile
app/Makefile
app/pixmaps/Makefile
samples/Makefile
)
